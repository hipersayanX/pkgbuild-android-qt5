# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>

_android_arch=x86

pkgname=android-${_android_arch}-libiconv
pkgver=1.16
pkgrel=1
pkgdesc="Provides GNU libiconv.so and libcharset.so (android)"
arch=('any')
url="http://www.gnu.org/software/libiconv/"
license=("LGPL")
depends=('android-ndk')
options=(!strip !buildflags staticlibs !emptydirs)
makedepends=('android-configure')
source=("http://ftp.gnu.org/pub/gnu/libiconv/libiconv-$pkgver.tar.gz")
md5sums=('7d2a800b952942bb2880efb00cfd524c')

build() {
    cd "${srcdir}"/libiconv-${pkgver}
    source android-env ${_android_arch}

    android-${_android_arch}-configure \
      --disable-nls
    make $MAKEFLAGS
}

package() {
    cd "${srcdir}"/libiconv-${pkgver}
    source android-env ${_android_arch}

    make DESTDIR="$pkgdir" install
    rm -r "${pkgdir}"/${ANDROID_PREFIX_BIN}
    rm -r "${pkgdir}"/${ANDROID_PREFIX_SHARE}
    ${ANDROID_STRIP} -g --strip-unneeded "${pkgdir}"/${ANDROID_PREFIX_LIB}/*.so
    ${ANDROID_STRIP} -g "$pkgdir"/${ANDROID_PREFIX_LIB}/*.a
}
